//
//  Fiscal closure where the government spending on goods adjusts in 
//  response to spending and revenue keeping the deficit set exogenously.
//
//

variable GOVS(regions)        'government spending excluding labor'        end,gdp,logged ;
variable GCE(goods_o,regions) 'government purchases of goods and services' end,gdp,logged ;

// Government spending excluding labor set to allow deficit to be determined exogenously.

GOVS = ln( DEFI - (exp(WAGG-PRID)*GOVL) - TRAN + exp(TAXT) - INTR*BOND );

//  Government spending on specific goods

goods_e: GCE = ln(delta_eG) + ln(delta_oG(e)/(delta_oG(e)+delta_oG(m))) + GOVS + PRID - PRY ;
goods_m: GCE = ln(delta_mG) + ln(delta_oG(m)/(delta_oG(e)+delta_oG(m))) + GOVS + PRID - PRY ;

// government spending on all goods

GCET = ln( sum(goods_o,exp(GCE + PRY(goods_o))) ) - PRID ;

// Total government spending

GOVT = ln( exp(GCET) + (exp(WAGG-PRID)*GOVL) ) ;

//  Government transfers to consumers

TRAN = TRANX + transgdp*exp(OUTP) ;

// Tariff revenue by good

TXM = sum(orig,(TIM*MUL#goods_o + TIF*FTA#goods_o)*exp(PIM + IMP)) / exp(PRID) ;

//  Total tariff revenue

TAXM(dest) = sum(goods_o,TXM) ;

//  Total export taxes

TAXX = sum(goods_o, TEX(goods_o)*exp(PRS(goods_o) + EXQ)) / exp(PRID) ;

//
// Taxes on households
//

TAXH = ln(
          TINC*( 
               exp(WAGE+IITL)
             + exp(WAGE+CNPL)
             + exp(WAGG)*GOVL
             + sum(sec_std,exp(WAG+LAB)) 
          )
        - TITCH*exp(PRII+INVZ)
     ) - PRID ;

// TAXC equation in the TXGS module.

//
//  Evolution of stock of government bonds
//

lead(BOND) = DEFI + BOND*(1-labgrow) - PBAG ;

// The lump sum tax is equal to the debt servicing costs plus and exogenous adjustment.

TAXL = INTR*BOND + TAXS ;

//  Total tax collections

TAXT = ln( exp(TAXC) + exp(TAXH) + (TAXL + TAXM + TAXX + TAXE) ) ;

//  DEFI: Government deficit at INTR

DEFI = DEFX - TAXS ;

//  DEFN: Government deficit at INTN

DEFN = DEFI + (INTN - INTR)*BOND ;