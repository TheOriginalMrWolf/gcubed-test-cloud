//
//  Standard monetary policy module
//
//  Change the setparameters.csv file to alter this monetary rule.
//

variable INTL(regions)            'interest rate, short-run, nominal (lagged)'   sta,pct ;
variable INPL(regions)            'desired interest rate, short-run,  (lagged)'  sta,pct ;
variable PRCOL(regions)           'household price of material - lagged'         sta,idx,logged,lagged ;

variable INFL(regions)            'inflation rate in PRCT'                       end,pct ;
variable INFCL(regions)           'inflation rate in PRCO (core)'                end,pct ;
variable YGRO(regions)            'output growth'                                end,pct ;
variable GNEN(regions)            'GNE, nominal'                                 end,gdp,logged ;
variable GNER(regions)            'GNE, real'                                    end,gdp,logged ;
variable EYGR(regions)            'expected output growth'                       end,pct,intertemporal_constant ;
variable EPRC(regions)            'expected PRCT inflation rate'                 end,pct,intertemporal_constant ;

variable PTAR(regions)            'price level target'                           exo,idx,logged ;
variable INFX(regions)            'inflation target'                             exo,pct ;
variable INTX(regions)            'shock to interest rate target'                exo,del ;
variable EXCX(regions)            'exchange rate target percentage change'       exo,pct ;

EYGR =  lead(OUTP) - OUTP ;

EPRC = lead(PRCT) - PRCT ;

INFL = PRCT - PRCL ;

lead (PRCOL) = PRCO ;

INFCL = PRCO - PRCOL ;

YGRO = OUTP - OUTL ;

GNEN = ln( 
            exp(PRCT+CONP)
          + exp(PRID+GCET)
          + exp(PRII+INVT) 
          + sum(goods_o,exp(PRII)*INS)
     ) ;

GNER = ln( 
            exp(CONP)
          + exp(GCET)
          + exp(INVT)
          + sum(goods_o,INS)
     ) ;

INPN =    mrule_r*INPL
        + mrule_ex*(EXCH - EXCL - EXCX)                                  //Exchange rate
        + mrule_1*(INFL - INFX)                                          //CPI inflation
        + mrule_2*(INFP - INFX)                                          //PPI inflation
        + mrule_3*(INFCL - INFX)                                         //core inflation
        + mrule_4*(PRCT - PTAR)                                          //PRC level
        + mrule_5*(PRID - PTAR)                                          //PPI level
        + mrule_6*(WAGE - PTAR)                                          //wage level
        + mrule_7*(0)                                                    //wage growth
        + mrule_8*((OUTP - OUTL) - ROGY)                                 //real output growth
        + mrule_9*((OUTP - OUTL) - ROGY + INFP - INFX)                   //nominal gross output growth
        + mrule_10*(GDPN - ROGY)                                         //nominal GDP
        + mrule_11*(OUTP + PRID)                                         //nominal gross output
        + mrule_12*(ln(exp(OUTP+PRID) - TBAL))                           //nominal gross output net of trade
        + mrule_13*(EPRC - INFX)                                         //expected CPI inflation
        + mrule_14*(EYGR - ROGY)                                         //expected real output growth
        + mrule_15*(GNEN - ROGY)                                         //nominal GNE
         ;


// Use a partial adjustment mechanism with central banks lagging the exact policy rule.
INTN = INTL + mrule_m*(INPN - INTN)+ INTX ;

// Alternatively, use the exact policy rule for INTN.
// INTN = INPN + INTX ;

lead(INTL) = INTN ;

lead(INPL) = INPN ;