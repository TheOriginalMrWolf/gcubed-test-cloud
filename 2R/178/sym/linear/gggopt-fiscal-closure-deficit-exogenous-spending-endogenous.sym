//
//  Fiscal closure where the government spending on goods adjusts in 
//  response to spending and revenue keeping the deficit set exogenously.
//
//

variable GOVS(regions)        'government spending excluding labor'        end,gdp ;
variable GCE(goods_o,regions) 'government purchases of goods and services' end,gdp ;

// Government spending excluding labor set to allow deficit to be determined exogenously.

GOVS = DEFI - GOVL*exp(WAGG-PRID) - TRAN + TAXT - INTR*BOND ;

//  Government spending on specific goods

goods_e: GCE = delta_eG*(delta_oG(e)/(delta_oG(e)+delta_oG(m))) * GOVS * exp(PRID) / exp(PRY) ;
goods_m: GCE = delta_mG*(delta_oG(m)/(delta_oG(e)+delta_oG(m))) * GOVS * exp(PRID) / exp(PRY) ;

// government spending on all goods

GCET = sum(goods_o,GCE*exp(PRY(goods_o))) / exp(PRID) ;

// Total government spending

GOVT = GCET + GOVL*exp(WAGG-PRID) ;

//  Government transfers to consumers

TRAN = TRANX + transgdp*OUTP ;

// Tariff revenue by good

TXM = sum(orig,(TIM*MUL#goods_o + TIF*FTA#goods_o)*exp(PIM)*IMP) / exp(PRID) ;

//  Total tariff revenue

TAXM(dest) = sum(goods_o,TXM) ;

//  Total export taxes

TAXX = sum(goods_o, TEX(goods_o)*exp(PRS(goods_o))*EXQ) / exp(PRID) ;

//
// Taxes on households
//

TAXH = (
          TINC*( 
                 exp(WAGE)*(IITL+CNPL)
               + exp(WAGG)*(GOVL)
               + sum(sec_std,exp(WAG)*LAB)
          )
        - (TITCH*exp(PRII)*INVZ)
     ) / exp(PRID) ;

// TAXC equation in the TXGS module.

//
//  Evolution of stock of government bonds
//

lead(BOND) = DEFI + BOND*(1-labgrow) - PBAG ;

// The lump sum tax is equal to the debt servicing costs plus and exogenous adjustment.

TAXL = INTR*BOND + TAXS ;

//  Total tax collections

TAXT = TAXC + TAXH + TAXL + TAXM + TAXX + TAXE ;

//  DEFI: Government deficit at INTR

DEFI = DEFX - TAXS ;

//  DEFN: Government deficit at INTN

DEFN = DEFI + (INTN - INTR)*BOND ;