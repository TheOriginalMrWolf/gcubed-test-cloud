//
//  Fiscal closure 2.
//  
//  The government deficit and total government spending are determines exogenously.
//  The fiscal deficit, DEFI is held fixed by adjusting the lump sum tax 
//  as necessary.
//

variable GOVS(regions)        'government spending excluding labor'        exo,gdp ;
variable GCE(goods_o,regions) 'government purchases of goods and services' end,gdp ;

//  Government spending on specific goods

goods_e: GCE = delta_eG*(delta_oG(e)/(delta_oG(e)+delta_oG(m))) * GOVS * exp(PRID) / exp(PRY) ;
goods_m: GCE = delta_mG*(delta_oG(m)/(delta_oG(e)+delta_oG(m))) * GOVS * exp(PRID) / exp(PRY) ;

// government spending excluding labor

GCET = sum(goods_o,GCE*exp(PRY(goods_o))) / exp(PRID) ;

// Total government spending

GOVT = GCET + GOVL*exp(WAGG-PRID) ;

//  Government transfers to consumers

TRAN = TRANX + transgdp*OUTP ;

// Tariff revenue by good

TXM = sum(orig,(TIM*MUL#goods_o + TIF*FTA#goods_o)*exp(PIM)*IMP) / exp(PRID) ;

//  Total tariff revenue

TAXM(dest) = sum(goods_o,TXM) ;

//  Total export taxes

TAXX = sum(goods_o, TEX(goods_o)*exp(PRS(goods_o))*EXQ) / exp(PRID) ;

//
// Taxes on households
//

TAXH = (
          TINC*( 
                 exp(WAGE)*(IITL+CNPL)
               + exp(WAGG)*(GOVL)
               + sum(sec_std,exp(WAG)*LAB)
          )
        - (TITCH*exp(PRII)*INVZ)
     ) / exp(PRID) ;

// TAXC equation in the TXGS module.

//
//  Evolution of stock of government bonds
//

lead(BOND) = DEFI + BOND*(1-labgrow) - PBAG ;

// The lump sum tax  to hold the deficit equal to DEFX - TAXS

TAXL = GOVT + TRAN - (TAXC + TAXH + TAXM + TAXX + TAXE) + INTR*BOND - DEFI + TAXS ;

//  Total tax collections

TAXT = TAXC + TAXH + TAXL + TAXM + TAXX + TAXE ;

//  DEFI: Government deficit at INTR

DEFI = DEFX - TAXS ;

//  DEFN: Government deficit at INTN

DEFN = DEFI + (INTN - INTR)*BOND ;
